import "../devices/led.thingml"
import "../devices/softtimer.thingml"
import "../devices/softbutton.thingml"

thing fragment MyButton includes SoftButtonMsgs {
	required port softButton {
		receives click, doubleclick, longpress
	}
}

thing fragment TimerClient includes TimerMsgs {
	required port timer {
		sends timer_start, timer_cancel
		receives timer_timeout
	}
}

thing fragment Led includes LEDMsgs {
	required port led {	
		sends led_toggle, led_off
	}
}

thing BlinkSoftButton includes Led, MyButton, TimerClient {

    statechart BlinkSoftButtonImpl init Running {
        
        composite state Running init Wait { 
        
   			transition -> BlinkFast
   			event softButton?longpress
   			action led!led_toggle()

	        state Wait {  
	        	on entry led!led_off()
	        
	        	internal event softButton?click
	        	action led!led_toggle()
	        	
	        	transition -> BlinkSlow
	        	event softButton?doubleclick
	        	action led!led_toggle()         
	        }
	        
	        state BlinkSlow {
	        	on entry timer!timer_start(1000)
	        	
	        	transition -> BlinkSlow
	        	event timer?timer_timeout
	        	action led!led_toggle()
	        	
	        	transition -> Wait
	        	event softButton?click 
	        }
        }
        
        state BlinkFast {
        	on entry timer!timer_start(200)
        	
        	transition -> BlinkFast
	       	event timer?timer_timeout
	        action led!led_toggle()
        	
        	transition -> Running
        	event softButton?click
        }
    }
}

configuration SoftButton {       
	
	instance arduino : Arduino
	
	// SoftButton device configuration.
	instance button : Button
		set pin = DigitalPin:PIN_4
	instance sbutton : SoftButton
	instance timer : SoftTimer
	connector button.DigitalIO => arduino.DigitalIO
	connector sbutton.button => button.Button
	connector sbutton.timer => timer.timer
	
	// LED device configuration.
	instance led : LED
		set pin = DigitalPin:PIN_13
	connector app.led => led.LED
	connector led.DigitalIO => arduino.DigitalIO
	
	// App configuration.
	instance myTimer : SoftTimer 	
	instance app : BlinkSoftButton
	connector app.softButton => sbutton.softButton
	connector app.timer => myTimer.timer
}