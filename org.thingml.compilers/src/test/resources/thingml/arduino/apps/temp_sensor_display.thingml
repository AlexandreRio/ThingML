import "../devices/lcd_screen.thingml"
import "../devices/analog_input.thingml"

thing TempSensorDisplay includes LcdScreenMsgs, AnalogPotMsgs
@c_header "
#include <math.h>
int temperature (int RawADC) {
 double Temp;
 Temp = log(((10240000/RawADC) - 10000));
 Temp = 1 / (0.001129148 + (0.000234125 + (0.0000000876741 * Temp * Temp ))* Temp );
 Temp = Temp - 273.15;            // Convert Kelvin to Celcius
 return (int)Temp;
}
"
{
	required port Display 
    {	
		sends initDisplay, refreshDisplay, getDisplay
	}

	required port Pot
	{
		receives value
		sends subscribe
	}
	
   	statechart TempSensorDisplayImpl init Running
   	{
   		on entry Display!initDisplay ("Temperature", "C", 0, 20, 35)
   		state Running
   		{
   			on entry Pot!subscribe (true)
   			internal refresh event m : Pot?value
   			action do
   				Display!refreshDisplay (0, 'temperature ('& m.val &')')
   				Display!getDisplay (0)
   				end 
   		}	
   	}
}

configuration TempSensorDisplay includes AnalogInputConfFrag
{
	instance screen : LcdScreen
	instance app : TempSensorDisplay
	
	connector app.Display => screen.Display
	connector app.Pot => pot.Pot
}