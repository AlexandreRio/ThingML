import "../core/arduino.thingml"

thing fragment LEDMsgs {
	message led_on();
	message led_off();
	message led_toggle();
}

thing LED includes LEDMsgs, ArduinoStdlibMsgs
{
    
    readonly property pin : DigitalPin = DigitalPin:PIN_13
    
	provided port LED 
    {
		receives led_on, led_off, led_toggle
	}

    required port DigitalIO
    {
        sends pinMode, digitalWrite    
    }
    
    statechart LEDImpl init LED_OFF {
        
        on entry DigitalIO!pinMode(pin, PinMode:OUTPUT)
        
        state LED_OFF {
            on entry DigitalIO!digitalWrite(pin, DigitalState:LOW)
            transition switch_on -> LED_ON event LED?led_on event LED?led_toggle
            
        }
        
        state LED_ON {
            on entry DigitalIO!digitalWrite(pin, DigitalState:HIGH)
            transition switch_off -> LED_OFF event LED?led_off event LED?led_toggle
        }
    
   
    }
    
}