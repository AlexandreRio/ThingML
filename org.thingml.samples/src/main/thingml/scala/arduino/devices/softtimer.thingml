import "../../thingml.thingml"

datatype JavaTimer
@java_type "java.util.Timer";

datatype JavaTimerTask
@java_type "java.util.TimerTask";

// Manage a set of software timers.
thing SoftTimer includes Timer, ThingMLSchedulerMsg
@scala_trait "org.thingml.utils.TimerTaskTrait"
{

    property javaTimer : JavaTimer = 'new java.util.Timer()'
    property lastTask : JavaTimerTask
	
    required port Polling {
		receives poll
	}

    function run()@override "true" : Void
    do
        timer!timer_timeout()
    end
    
    function cancel() : Void
    do
      'try {'
      if (not(lastTask == 'null')) 
      do
      '' & lastTask & '.cancel'            
      '' & javaTimer & '.purge'
      end
      '} catch {'
      'case _ =>'
      javaTimer = 'new java.util.Timer()'
      '}'
    end
    
    function start(delay : Integer) : Void
    do
        'cancel()'
        lastTask = 'this.newTimerTask'
        'try {'
        '' & javaTimer & '.schedule(' & lastTask & ', ' & delay & ')'
        '} catch {'
        'case _ =>'
        '}'
    end
    
    statechart SoftTimer init default {
    
        internal start
            event m : timer?timer_start
            guard m.delay > 0
            action do //should be implemented as proper ThingML function calls (currently not supported by compiler)
                'start(' & m.delay & ')'
            end
            
        internal cancel
            event m : timer?timer_cancel
            action 'cancel()' //should be implemented as proper ThingML function calls (currently not supported by compiler)
    
        state default {}
        
    }
}