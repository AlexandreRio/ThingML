import "../core/arduino.thingml"
import "softtimer.thingml"

thing fragment AnalogPotMsgs
{
	message getValue ();
	message subscribe (value : Boolean);
	message value (val : Int16);
}

thing AnalogPot includes AnalogPotMsgs, TimerMsgs, ArduinoStdlibMsgs
{
	readonly property pin : UInt8 = 1
	readonly property refreshRate : UInt16 = 30
	readonly property precision : Int8 = 1
	
	property oldValue : Int16 = 3333
	
	required port Timer
	{
		sends timer_start, timer_cancel
		receives timer_timeout
	}
	
	required port AnalogIO {
		sends analogRead
		receives analogReadResult
	}

	provided port Pot
	{
		sends value
		receives subscribe, getValue
	} 
	
	statechart AnalogPotImpl init Running
	{ 	 
		composite state Running init Unsubscribe keeps history
		{ 
			transition -> ReadValueG
			event Pot?getValue
            action AnalogIO!analogRead (pin)
            
            transition -> ReadValueS
			event Timer?timer_timeout
			action AnalogIO!analogRead (pin)
				
			state Unsubscribe
			{
				transition -> Subscribe
				event m : Pot?subscribe
				guard m.value == true
			}
			
			state Subscribe
			{
				on entry Timer!timer_start (refreshRate)
				
				transition -> Unsubscribe
				event m : Pot?subscribe
				guard m.value == false
				action Timer!timer_cancel ()
			}
		}
		
		state ReadValueS
		{	
			transition -> Running
			event m : AnalogIO?analogReadResult
			action do
				if (m.value - oldValue > precision or m.value - oldValue < - precision) do
                    Pot!value (m.value)
				    oldValue = m.value
                end 
			end
		}
        
        state ReadValueG
		{	
			transition -> Running
			event m : AnalogIO?analogReadResult
			action Pot!value (m.value)
		}
	}
}

configuration AnalogInputConfFrag
{
	instance arduino : Arduino
	instance pot : AnalogPot
        set pin = 4
	instance timer : SoftTimer
	connector pot.Timer => timer.timer
	connector pot.AnalogIO => arduino.AnalogIO
}