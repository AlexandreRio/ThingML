import "../devices/lcd_screen.thingml"
import "../devices/analog_input.thingml"

thing TempSensorDisplay includes LcdScreenMsgs, AnalogInputMsgs
@c_header "
#include <math.h>
int temperature (int RawADC) {
 double Temp;
 Temp = log(((10240000/RawADC) - 10000));
 Temp = 1 / (0.001129148 + (0.000234125 + (0.0000000876741 * Temp * Temp ))* Temp );
 Temp = Temp - 273.15;            // Convert Kelvin to Celcius
 return (int)Temp;
}
"
{
	required port Display 
    {	
		sends initDisplay, refreshDisplay, setDisplay
	}

	required port Temp
	{
		receives analogInputValue
		sends readAnalogInput
	}
	
   	statechart TempSensorDisplayImpl init Running
   	{
   		on entry do
                Display!initDisplay (0, "Temperature", "C", 0, 20, 35)
                Display!setDisplay (0)
                end
   		state Running
   		{
   			internal refresh event m : Temp?analogInputValue
   			action Display!refreshDisplay (0, 'temperature ('& m.value &')') 
   		}	
   	}
}

configuration TempSensorDisplay
{
    // Arduino declaration.
    instance arduino : Arduino
    
    // On change analog input configuration.
	instance on_change_analog_input : OnChangeAnalogInput
        set pin = AnalogPin:A_3
	instance timer : SoftTimer
	connector on_change_analog_input.AnalogIO => arduino.AnalogIO
	connector on_change_analog_input.Timer => timer.timer

    // Screen declaration.
	instance screen : LcdScreen

    // Application configuration.
	instance app : TempSensorDisplay	
	connector app.Display => screen.Display
	connector app.Temp => on_change_analog_input.AnalogInput
}