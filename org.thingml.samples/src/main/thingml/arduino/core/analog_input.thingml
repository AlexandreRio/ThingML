import "../core/arduino.thingml"
import "arduino_timer.thingml"

thing fragment AnalogInputMsgs
{
	message read_analog_input ();
	message analog_input_value (value : Int16);
}

thing fragment AnalogInput includes AnalogInputMsgs, ArduinoStdlibMsgs
{
	readonly property pin : AnalogPin = AnalogPin:A_4
	
	provided port AnalogInput 
	{
		receives read_analog_input
		sends analog_input_value 
	}
	
	required port AnalogIO
	{
		receives analog_read_result
		sends analog_read
	}
}

thing SimpleAnalogInput includes AnalogInput
{
	statechart SimpleAnalogInputImp init Running
	{
		state Running
		{
			transition WaitingResult -> Waiting
			event AnalogInput?read_analog_input
			action AnalogIO!analog_read (pin)
		}
		
		state Waiting
		{
			transition ResultCatched -> Running
			event m : AnalogIO?analog_read_result
			guard m.pin == pin
			action AnalogInput!analog_input_value (m.value)
		}
	}
}

thing PeriodicAnalogInput includes AnalogInput, TimerMsgs
{
	readonly property refresh_rate : Long = 5000

	required port Timer
	{
		receives timer_timeout
		sends timer_start
	}
	statechart PeriodicAnalogInputImp init Running
	{	
		state Running
		{
			on entry Timer!timer_start (refresh_rate)
			
			transition wait -> Waiting
			event Timer?timer_timeout
			event AnalogInput?read_analog_input
			action AnalogIO!analog_read (pin)
		}
		
		state Waiting
		{
			transition got -> Running 
			event m : AnalogIO?analog_read_result
			guard m.pin == pin
			action AnalogInput!analog_input_value (m.value)
		}
	}
}

thing OnChangeAnalogInput includes AnalogInput, TimerMsgs
{
	readonly property value_precision : UInt8 = 1
	readonly property refresh_rate : UInt8 = 20
	
	property oldValue : Int16 = 3333
	
	required port Timer
	{
		sends timer_start
		receives timer_timeout
	}
	
	statechart OnChangeAnalogInputImpl init Running
	{
		state Running
		{
			on entry Timer!timer_start (refresh_rate)
			
			transition wait -> Waiting 
			event Timer?timer_timeout
			action AnalogIO!analog_read (pin)
		} 
		
		state Waiting
		{
			transition got -> Running
			event m : AnalogIO?analog_read_result
			guard m.pin == pin
			action do
                if (m.value - oldValue < - value_precision or m.value - oldValue > value_precision) do
				    AnalogInput!analog_input_value (m.value)
				    oldValue = m.value
                    end
			end
		}	 
	
	 	region SimpleAnalogRead init Idle 
	 	{
        	state Idle 
        	{ 
        		transition waiting_answer -> Waiting 
        		event AnalogInput?read_analog_input
        		action AnalogIO!analog_read (pin)
        	}
            
            state Waiting 
            {
                transition GotAnswer -> Idle 
                event m: AnalogIO?analog_read_result 
                guard m.pin == pin
                action AnalogInput!analog_input_value (m.value)
            }    
        }	
	}	
}

configuration fragment SimpleAnalogInput
{
    instance arduino : Arduino

    instance analog_input : SimpleAnalogInput
    connector analog_input.AnalogIO => arduino.AnalogIO
}

configuration fragment PeriodicAnalogInput
{
    instance arduino : Arduino
    
    instance analog_input : PeriodicAnalogInput
    instance timer : SoftTimer
    connector analog_input.AnalogIO => arduino.AnalogIO
    connector analog_input.Timer => timer.timer
}

configuration fragment OnChangeAnalogInput
{
    instance arduino : Arduino
    
    instance analog_input : OnChangeAnalogInput
    instance timer : SoftTimer
    connector analog_input.AnalogIO => arduino.AnalogIO
    connector analog_input.Timer => timer.timer
} 