import "../core/arduino.thingml"
import "../devices/analog_input.thingml"

thing fragment LuxSensorMsgs includes AnalogInputMsgs 
{
	message read_luminosity ();
	message luminosity_value (val : UInt16);
}

thing LuxSensor includes LuxSensorMsgs
{
    readonly property pin : AnalogPin = AnalogPin:A_5
    readonly property sample_rate : UInt8 = 1
    property tot_value : UInt16 = 0
    property counter : UInt8 = 0
    
	provided port LuxSensor 
    {
		receives read_luminosity
		sends luminosity_value
	}

    required port AnalogInput
    {
        receives analog_input_value
        sends read_analog_input    
    }
    
    statechart LuxSensorImpl init Running 
    {    
    	internal event m : AnalogInput?analog_input_value
        guard counter == sample_rate
    	action do
            LuxSensor!luminosity_value ((tot_value + m.value) / (sample_rate + 1))
            tot_value = 0
            counter = 0
            end
            
        internal event m : AnalogInput?analog_input_value
        guard counter < sample_rate
    	action do
            tot_value = tot_value + m.value
            counter = counter + 1
            end
    	
        state Running 
        {
        	internal event LuxSensor?read_luminosity
        	action AnalogInput!read_analog_input ()
        }        
    }
}

//* Configuration needs:
//
// includes AnalogInput
//
// instance lux_sensor : LuxSensor
// instance analog_input : OnChangeAnalogInput
// 		set pin = pin	 
// 
// connector lux_sensor.AnalogInput => analog_input.AnalogInput
//
//* 