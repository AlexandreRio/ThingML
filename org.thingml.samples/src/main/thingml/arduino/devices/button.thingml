import "../core/arduino.thingml"

thing fragment ButtonMsgs {
	message press();
	message release();
}

thing Button includes ButtonMsgs, ArduinoMsgs
{	

	readonly property pin : DigitalPin = DigitalPin:PIN_3
	
	// Events generated by the buttons
	provided port Button {
		sends press
		sends release
	}
	
	required port DigitalIO
    {
        sends pinMode, digitalRead, digitalWrite
		receives digitalReadResult    
    }
	
	required port Polling {
		receives loop
	}
	
	 statechart ButtonImpl init Released {
        
        property debounce : Int8 = 20
        
        on entry do
        	DigitalIO!pinMode(pin, PinMode:INPUT)
        	DigitalIO!digitalWrite(pin, DigitalState:HIGH) // pull-up
        	debounce = 20
        end
        
        internal event Polling?loop
            action do
            	if (debounce > 0) debounce = debounce - 1
            	if (debounce == 0) DigitalIO!digitalRead(pin)
            end
        
        state Released {
            transition pressed -> Pressed
            event m: DigitalIO?digitalReadResult
            guard m.value == DigitalState:HIGH
            action do 
            	debounce = 20
            	Button!press()
            end
        }
        
        state Pressed {
            transition pressed -> Released
            event m: DigitalIO?digitalReadResult
            guard m.value == DigitalState:LOW
            action do
            	debounce = 20
            	Button!release()
            end
        }
    }
}