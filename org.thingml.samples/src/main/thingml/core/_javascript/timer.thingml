import "../timer.thingml"

// Manage a set of software timers.
thing TimerJS includes Timer
@pim "Timer"
@platform "javascript"
@javascript "node browser"
@debug "true"
{
    function cancel() @debug "false" 
    do
        'clearTimeout(this.timer);'
    end
    
    function start(delay : Integer)
    do
        'this.timer = setTimeout(onTimeout,' & delay & ');'
    end
    
    function onTimeout() 
    do
        timer!timer_timeout()
    end
    
    statechart SoftTimer init default {
        state default {
            
          internal start
            event m : timer?timer_start
            guard m.delay > 0
            action do
                start(m.delay)
            end
            
          internal cancel
            event m : timer?timer_cancel
            action cancel()    
        }
    }
}

/**
* @debug "true" on configuration indicates we should traces all instances. 
* To debug a given instance, put the annotation on the instance, and not on the configuration. 
* Alternatively, keep the annotation on the configuration and put @debug "false" on the instances you do not want to debug
*/
configuration TestTimerJS @debug "true"{
    instance timer : TimerJS 
    instance client : SimpleTimerClient
    connector client.timer => timer.timer    
}
