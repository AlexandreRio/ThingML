//*
// Fabien Fleurey - 22/08/2011
// Electronic bricks library.
//
// This file provides an interface with the LED. 
//*

import "../led.thingml"
import "../../io/_arduino/digital_output.thingml"

thing LedC includes Led, DigitalOutputMsgs
@pim "Led"
@platform "Arduino"
{   
    required port DigitalOutput
    {
        sends set_digital_output    
    }
    
    statechart LedImpl init LedOff 
    {    
        state LedOff 
        {
            on entry DigitalOutput!set_digital_output (DigitalState:LOW)
            transition switch_on -> LedOn 
            event Led?led_on 
            event Led?led_toggle
            
        }
        
        state LedOn 
        {
            on entry DigitalOutput!set_digital_output (DigitalState:HIGH)
            transition switch_off -> LedOff 
            event Led?led_off 
            event Led?led_toggle
        }
    }
}

configuration fragment LedArduino
{
    group io : DigitalOutputArduino

    instance led : Led	 
    connector led.DigitalOutput => io.digital_output.DigitalOutput
} 